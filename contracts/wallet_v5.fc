#pragma version =0.4.4;

#include "imports/stdlib.fc";

;; Extensible wallet contract v5

;; Compresses 8+256-bit address into 256-bit uint by cutting off one bit from sha256.
;; This allows us to save on wrapping the address in a cell and make plugin requests cheaper.
;; This method also unpacks address hash if you pass packed hash with the original wc.
int pack_address(int wc, int hash) impure asm "SWAP INC XOR"; ;; hash ^ (wc+1)

;; Stores pre-computed list of actions (mostly `action_send_msg`) in the actions register.
() set_actions(cell action_list) impure asm "c5 POP";


() dispatch_request1(slice cs, int stored_seqno, int stored_subwallet, int public_key, cell extensions) impure inline {
    ;; Recurse into extended actions until we reach standard actions
    while (cs~load_uint(1)) {
        int op = cs~load_uint(32);

        ;; Raw set_data
        if (op == 0x1ff8ea0b) {
            set_data(cs~load_ref());
        } 

        if (op == 0x1c40db9f){
            ;; Add extension
            (int wc, int hash) = parse_std_addr(cs~load_msg_addr());
            int packed_addr = pack_address(wc, hash);
            throw_unless(39, extensions~udict_add_builder?(256, packed_addr, begin_cell().store_int(wc,8)));
            set_data(begin_cell()
            .store_uint(stored_seqno, 32)
            .store_uint(stored_subwallet, 80)
            .store_uint(public_key, 256)
            .store_dict(extensions)
            .end_cell());
        } 
        if (op == 0x5eaef4a4) {
            ;; Remove extensions
            (int wc, int hash) = parse_std_addr(cs~load_msg_addr());
            int packed_addr = pack_address(wc, hash);
            throw_unless(40, extensions~udict_delete?(256, packed_addr));
            set_data(begin_cell()
            .store_uint(stored_seqno, 32)
            .store_uint(stored_subwallet, 80)
            .store_uint(public_key, 256)
            .store_dict(extensions)
            .end_cell());
        }
        ;; Other actions are no-op
        ;; FIXME: is it costlier to check for unsupported actions and throw?
        cs = cs~load_ref().begin_parse();
    }
    ;; At this point we are at `action_list_basic$0 {n:#} actions:^(OutList n) = ActionList n 0;`
    ;; Simply set the C5 register with all pre-computed actions:
    set_actions(cs~load_ref());
    return ();
}

;; Dispatches already authenticated request.
() dispatch_request(slice cs, slice data, cell extensions) impure inline {
    ;; Recurse into extended actions until we reach standard actions
    while (cs~load_uint(1)) {
        int op = cs~load_uint(32);

        ;; Raw set_data
        if (op == 0x1ff8ea0b) {
            set_data(cs~load_ref());
        } 

        if (op == 0x1c40db9f){
            ;; Add extension
            (int wc, int hash) = parse_std_addr(cs~load_msg_addr());
            int packed_addr = pack_address(wc, hash);
            throw_unless(39, extensions~udict_add_builder?(256, packed_addr, begin_cell().store_int(wc,8)));
            set_data(begin_cell()
            .store_slice(data)
            .store_dict(extensions)
            .end_cell());
        }
        if (op == 0x5eaef4a4) {
            ;; Remove extensions
            (int wc, int hash) = parse_std_addr(cs~load_msg_addr());
            int packed_addr = pack_address(wc, hash);
            throw_unless(40, extensions~udict_delete?(256, packed_addr));
            set_data(begin_cell()
            .store_slice(data)
            .store_dict(extensions)
            .end_cell());
        }

        ;; Other actions are no-op
        ;; FIXME: is it costlier to check for unsupported actions and throw?
        cs = cs~load_ref().begin_parse();
    }
    ;; At this point we are at `action_list_basic$0 {n:#} actions:^(OutList n) = ActionList n 0;`
    ;; Simply set the C5 register with all pre-computed actions:
    set_actions(cs~load_ref());
    return ();
}

;; Verifies signed request, prevents replays and proceeds with `dispatch_request`.
;; () process_signed_request(slice body, int stored_seqno, int stored_subwallet, int public_key, cell extensions) impure inline {
() process_signed_request(slice body, int stored_seqno, int stored_subwallet, int public_key, cell extensions) impure inline {
    var signature = body~load_bits(512);
    var h = slice_hash(body);
    throw_unless(35, check_signature(h, signature, public_key));

    var (subwallet_id, valid_until, msg_seqno) = (body~load_uint(80), body~load_uint(32), body~load_uint(32));
    throw_unless(33, msg_seqno == stored_seqno);
    throw_unless(34, subwallet_id == stored_subwallet);
    throw_if(36, valid_until <= now());
    accept_message();

    ;; Store and commit the seqno increment to prevent replays even if the subsequent requests fail.
    stored_seqno = stored_seqno + 1;

    ;; var data = begin_cell()
    ;; .store_uint(stored_seqno, 32)
    ;; .store_uint(stored_subwallet, 80)
    ;; .store_uint(public_key, 256)
    ;; .end_cell()
    ;; .begin_parse();

    set_data(begin_cell()
    .store_uint(stored_seqno, 32)
    .store_uint(stored_subwallet, 80)
    .store_uint(public_key, 256)
    .store_dict(extensions)
    .end_cell());

    commit();
    ;; dispatch_request(body, data, extensions);
    dispatch_request1(body, stored_seqno, stored_subwallet, public_key, extensions);
}

() recv_external(slice body) impure {
    int auth_kind = body~load_uint(32);
    if (auth_kind == 0x7369676E) { ;; "sign"
        var ds = get_data().begin_parse();
        var (stored_seqno, stored_subwallet, public_key, extensions) = (ds~load_uint(32), ds~load_uint(80), ds~load_uint(256), ds~load_dict());
        ds.end_parse();
        process_signed_request(body, stored_seqno, stored_subwallet, public_key, extensions);
    } else {
        ;; FIXME: probably need to throw here?
        return ();
    }
}


() recv_internal(int msg_value, cell full_msg, slice body) impure {
    var full_msg_slice = full_msg.begin_parse();
    var flags = full_msg_slice~load_uint(4);  ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddressInt ...
    if (flags & 1) {
        ;; ignore all bounced messages
        return ();
    }
    if (body.slice_bits() < 32) {
        ;; ignore simple transfers
        return ();
    }
    int auth_kind = body~load_uint(32);

    ;; We accept two kinds of authenticated messages:
    ;; - 0x6578746E "extn" authenticated by extension
    ;; - 0x7369676E "sign" authenticated by signature
    if (auth_kind == 0x6578746E) { ;; "extn"
        var ds = get_data().begin_parse();
        var (data, extensions) = (ds~load_bits(368), ds~load_dict());
        ds.end_parse();
        ;; Authenticate extension by its address.
        int packed_sender_addr = pack_address(parse_std_addr(full_msg_slice~load_msg_addr()));
        var (_, success?) = extensions.udict_get?(256, packed_sender_addr);
        if ~(success?) {
            ;; Note that some random contract may have deposited funds with this prefix,
            ;; so we accept the funds silently instead of throwing an error (wallet v4 does the same).
            return ();
        }
        dispatch_request(body, data, extensions);
    } elseif (auth_kind == 0x7369676E) { ;; "sign"
        var ds = get_data().begin_parse();
        var (stored_seqno, stored_subwallet, public_key, extensions) = (ds~load_uint(32), ds~load_uint(80), ds~load_uint(256), ds~load_dict());
        ds.end_parse();
        ;; Process the rest of the slice just like the signed request.
        process_signed_request(body, stored_seqno, stored_subwallet, public_key, extensions);
    }
}


;; Get methods

int seqno() method_id {
    return get_data().begin_parse().preload_uint(32);
}

int get_wallet_id() method_id {
    return get_data().begin_parse().skip_bits(32).preload_uint(80);
}

int get_public_key() method_id {
    var cs = get_data().begin_parse().skip_bits(32 + 80);
    return cs.preload_uint(256);
}

;; Returns raw dictionary (or null if empty) where keys are packed addresses and the `wc` is stored in leafs.
;; User should unpack the address using the same packing function using `wc` to restore the original address.
cell get_extensions() method_id {
    var ds = get_data().begin_parse().skip_bits(32 + 80 + 256);
    return ds~load_dict();
} 
